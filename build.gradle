buildscript {
    dependencies {
        classpath files('gradle/gradle-witness.jar')
    }
}

apply plugin: 'java'
apply plugin: 'witness'
apply plugin: 'maven-publish'

compileJava {
    sourceCompatibility = 1.7
    targetCompatibility = 1.7
}

repositories {
    mavenLocal()
    maven { url "http://4thline.org/m2" }
    maven { url 'https://mvn.freenetproject.org' }
    jcenter()
}

configurations {
    provided
}

sourceSets {
    main {
        compileClasspath += configurations.provided
        test.compileClasspath += configurations.provided
        test.runtimeClasspath += configurations.provided
    }
}

dependencies {
    compile group: 'org.fourthline.cling', name: 'cling-core', version: '2.1.0'
    compile group: 'org.fourthline.cling', name: 'cling-support', version: '2.1.0'
    compile group: 'org.freenetproject', name: 'fred', version: 'build+'
}

dependencyVerification {
    verify = [
            'org.fourthline.cling:cling-core:cf556bae1a8526626b139e1a3815a8c960c4c55bf1d24baae143ff8079f326fd',
            'org.fourthline.cling:cling-support:d090bff05f033f2d0d1da42ba9f367d1155b54af3b44237baddd0faeef5f6a1b',
            'org.seamless:seamless-util:eb663e3739d67137baab18e65ed2cdec28213a8871458323c3cc62da085cec3c',
            'org.seamless:seamless-http:05da30fa260cf53770fefbd46482c04f6a37e19b663fec282a4c4384c0def813',
            'org.seamless:seamless-xml:6d80a97918e4ae91ecb676f9cd056942f1565d981d054a461058a16096464298',
            'org.seamless:seamless-swing:653fa6fea357f1349075bdd94328fc9c0d285046d7deb25a56dc8a86513b64e0',
    ]
}

jar {
    manifest {
        attributes 'Plugin-Main-Class': 'plugins.UPnP2.UPnP2',
                   'Required-Node-Version': '1472'
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId 'org.freenetproject.plugins'
            artifactId "UPnP2"
            version "0.1"
            from components.java
        }
    }
    repositories {
        maven {
            url "s3://mvn.freenetproject.org/"
            credentials(AwsCredentials) {
                accessKey "${System.getenv('AWS_ACCESS_KEY_ID')}"
                secretKey "${System.getenv('AWS_SECRET_ACCESS_KEY')}"
            }
        }
    }
}
